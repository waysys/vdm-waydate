-- ----------------------------------------------------------------------------
--
-- AbsoluteDate - a date format for calculations
-- Author: William Shaffer (wshaffer@waysysweb.com)
-- Date: 19 Nov 2023
--
-- Absolute date is a way of specifying a date with a single nat1 number. 1
-- represent 1-Jan-1601.  Any date after that is an increment to that number.
-- Absolute dates support rapid calculation with dates.
-- ----------------------------------------------------------------------------

module AbsoluteDate

imports 
    from Year 
        values 
            MinYear : nat1 renamed MinYear; 

        functions 
            isYear : nat1 -> bool renamed isYear;

exports 
    types
        AbsoluteDate;

    values
        MaxAbsoluteDate : AbsoluteDate;
        MinAbsoluteDate : AbsoluteDate;

    functions
        yearFromAbsolute: AbsoluteDate -> nat;
        daysInPastYears: nat1 -> nat
        natToAbsoluteDate: nat1 -> AbsoluteDate;
        absoluteDateToNat: AbsoluteDate -> nat1;
        isAbsoluteDate: AbsoluteDate -> bool;

definitions

-- ----------------------------------------------------------------------------
types
-- ----------------------------------------------------------------------------

AbsoluteDate = nat1
inv absoluteDate == absoluteDate >= 1 and absoluteDate <= 876216
ord ad1 < ad2 == absoluteDateToNat(ad1) < absoluteDateToNat(ad2)

-- ----------------------------------------------------------------------------
values
-- ----------------------------------------------------------------------------

MinAbsoluteDate : AbsoluteDate = 1;
MaxAbsoluteDate : AbsoluteDate = 876216;

DaysIn400YearCycle = 400*365 + 100 - 3;
DaysIn100YearCycle = 365*100 + 25 - 1;
DaysIn4YearCycle   = 365*4 + 1;
DaysIn1YearCycle   = 365;

-- ----------------------------------------------------------------------------
functions
-- ----------------------------------------------------------------------------

-- isAbsoluteDate returns true if the value is between MinAbsoluteDate and 
-- MaxAbsoluteDate. 
isAbsoluteDate: AbsoluteDate -> bool 
isAbsoluteDate(value) == 
    if value < MinAbsoluteDate then false 
    elseif value > MaxAbsoluteDate then false 
    else true;

-- natToAbsoluteDate returns an absolute date confirming to the specified natural number.
-- This function is needed in other modules that import this module.
natToAbsoluteDate: nat1 -> AbsoluteDate
natToAbsoluteDate(value) ==
    value 
pre value >= 1 and value <= MaxAbsoluteDate;

-- absoluteDateToNat returns the natural number corresponding to the absolute date.
-- The function is needed in client modules that do calculations with absolute dates.
absoluteDateToNat: AbsoluteDate -> nat1
absoluteDateToNat(absoluteDate) ==
    absoluteDate;

-- yearFromAbsolute calculates the year associated with the absolute date.
--
-- This algorithm is from:
-- Edward M. Reingold and Nachum Dershowitz, Calendrical Calculations: The
-- Millennium Edition (Cambridge, UK: Cambridge University Press, 2001) p. 52
yearFromAbsolute: AbsoluteDate -> nat
yearFromAbsolute(absoluteDate) ==
    let
        num400YearCycles = (absoluteDate - 1) div DaysIn400YearCycle,
        remainder100 = (absoluteDate - 1) mod DaysIn400YearCycle,
        num100YearCycles = remainder100 div DaysIn100YearCycle,
        remainder4 = remainder100 mod DaysIn100YearCycle,
        num4YearCycles = remainder4 div DaysIn4YearCycle,
        remainder1 = remainder4 mod DaysIn4YearCycle,
        num1YearCycles = remainder1 div DaysIn1YearCycle
    in
        if ((num100YearCycles = 4) or (num1YearCycles = 4)) then
			400 * num400YearCycles + 100 * num100YearCycles + 4 * num4YearCycles + num1YearCycles + 1600
		else
			400 * num400YearCycles + 100 * num100YearCycles + 4 * num4YearCycles + num1YearCycles + 1601
pre isAbsoluteDate(absoluteDate)
post isYear(RESULT);


-- daysInPastYears computes the number of days starting in 1-Jan-1601 and ending 
-- in 31-Dec-year-1. Note that daysInPastYears(MinYear) = 0.
--
-- This alorithm is from:
-- Edward M. Reingold and Nachum Dershowitz, Calendrical
-- Calculations: The Millennium Edition (Cambridge, UK: Cambridge University Press, 2001) p. 51
daysInPastYears: nat1 -> nat
daysInPastYears(year) ==
    let
        y = year - MinYear
    in
        365 * y  -- days in prior years if all years had 365 days
             + y div 4    -- plus julian leap days in prior years if all years divided by 4 were leap years
             - y div 100  -- minus prior century years if all years divisible by 100 were not leap years
             + y div 400 -- plus years divisible by 400 which have leap days 
pre isYear(year) 
post RESULT >= 0;

end AbsoluteDate